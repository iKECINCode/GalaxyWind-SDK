//region 自定义配置
ext {
    customVersionCode = 1
    customVersionName = '1.0.0'
    customGroupId = 'com.ikecin.sdk' //只对本地maven有效，jitpack无效
    customArtifactId = 'demo'  //只对本地maven有效，jitpack无效
    //记得更改AndroidManifest.xml中的package
}
//endregion

// region 常规配置
apply plugin: 'com.android.library'

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 22
        versionCode customVersionCode
        versionName customVersionName
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'consumer-proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
}
// endregion

//region jitpack
apply plugin: 'com.github.dcendents.android-maven'
group = 'com.github.yikuo123'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    options {
        encoding 'utf-8'
        charSet 'utf-8'
    }
    exclude '**/BuildConfig.java'
    exclude '**/R.java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
//endregion

// region 发布到本地maven仓库
// 使用命令 gradlew uploadArchives 即可生成
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: rootDir.toPath().resolve('maven').toUri())
            pom.groupId = customGroupId
            pom.version = customVersionName
            pom.artifactId = customArtifactId
        }
    }
}
// endregion